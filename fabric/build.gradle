plugins {
    id 'ml-loader'
    id 'fabric-loom'
    id 'me.modmuss50.mod-publish-plugin'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}+${minecraft_version}"

    modImplementation "net.xstopho.resourceconfigapi:resourceconfigapi-fabric:${resource_config_api}"
    modImplementation "net.xstopho.resourcelibrary:resourcelibrary-fabric:${resource_library}"

    modImplementation "com.misterpemodder:shulkerboxtooltip-fabric:${shulker_box_tooltip}+${minecraft_version}"
    modImplementation "io.wispforest:accessories-fabric:${accessories}"

    // Runtime Mods
    modRuntimeOnly "maven.modrinth:modmenu:${mod_menu}"
    modRuntimeOnly "maven.modrinth:emi:${emi}+${minecraft_version}+fabric"
}

loom {
    var aw = project(":common").file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) accessWidenerPath.set(aw)

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            programArgs('--username', 'Dev')
            runDir("runs/client")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("runs/server")
        }
        datagen {
            inherit server
            name "Fabric Datagen"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"

            runDir "build/datagen"
        }
    }
}

// Loader Specific Settings
publishMods {
    file = remapJar.archiveFile
    modLoaders.addAll("fabric", "quilt")

    modrinth {
        requires('fabric-api')
    }
    curseforge {
        requires('fabric-api')
    }
}